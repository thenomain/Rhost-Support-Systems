
/*
--------------------------------------------------------------------------------
-- titlestr() ------------------------------------------------------------------

A complex function to make every word that should be capitalized, capitalized.
Warning: Sets the entire text lowercase to begin.

d.titlestr-notitle : Do not title these words
d.titlestr-punctuation : These indicate break points where the next word is 
        titled, even if it's on the notitle list.
d.titlestr-exception : Capitalize these words this way, exactly, no matter what.
        Good for acronyms or other reasons.

*/

&UFUNC.TITLESTR #udf=
    iter( 
        lcstr( %0 ), 
        case( 1, 
            t( 
                regmatch( 
                    %i0, 
                    u( #UDF/d.titlestr-regexp )
                )
            ), 
            %i0, 

            t( 
                setr( e, 
                    grab( 
                        u( #UDF/d.titlestr-punctuation ), 
                        escape( right( %i0, 1 ))
                    )
                )
            ), 
            [ulocal( #UDF/ufunc.titlestr, 
                strtrunc( %i0, dec( strlen( %i0 ))), 
                %1 
            )]
            %qe, 

            t( 
                setr( e, 
                    grab( 
                        [u( #UDF/d.titlestr-exception )] 
                        %1, 
                        %i0 
                    )
                )
            ), 
            %qe, 

            cand( 
                t( 
                    match( 
                        u( #UDF/d.titlestr-notitle ), 
                        %i0 
                    )
                ), 
                neq( inum( 0 ), 1 )
            ), 
            %i0, 

            t( 
                setr( e, 
                    grab( 
                        u( #UDF/d.titlestr-punctuation ), 
                        escape( strtrunc( %i0, 1 ))
                    )
                )
            ), 
            %qe
            [ulocal( #UDF/ufunc.titlestr, 
                rest( %i0, %qe ), 
                %1 
            )], 

            capstr( %i0 )
        )
    )
&HELP.TITLESTR #udf=
    titlestr( <string>[, <words to ignore>] ): 
    Outputs text in a 'title string' format, 
    will output <words to ignore> exactly as they are entered.

&D.TITLESTR-NOTITLE #udf=
    a an the at by for in of on to up and as but it or nor

&D.TITLESTR-PUNCTUATION #udf=
    . , ? ! ; : ( ) < > { } * / - + " '

&D.TITLESTR-REGEXP #udf=
    \[^[escape( u( d_titlestr-punctuation ))]\].*\\(.*\\)

&D.TITLESTR-EXCEPTION #udf=
    iPod von NPC BBS OOC IC EOD XP UFO DIY EMT EMTs FBI ASL EPD EU 
    DJ DJs TV VIP A&P DVD VCR PT


/* 
--------------------------------------------------------------------------------
-- cleanup ---------------------------------------------------------------------
*/

@switch/first version()=
	RhostMUSH*, { @@ do nothing }, 
	{ @fo me=@edit [v( d.udf )]/*=#UDF, [v( d.udf )] }

@trigger #udf/startup

